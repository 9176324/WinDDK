/*
    Copyright (c) Microsoft Corporation.  All rights reserved.
    
    Summary:
    Ndis Periodic timers should be cancelled at PASSIVE_LEVEL.
    
    Description:
        NdisCancelTimer is called at IRQL <= DISPATCH_LEVEL 
            if the timer IS NOT a periodic timer (MillisecondsPeriod = 0 in NdisSetTimerObject call)
                
        NdisCancelTimer is called at IRQL = PASSIVE_LEVEL
            if the timer IS a periodic timer (MillisecondsPeriod > 0 ), in this case the Timer is a "periodic timer"
    
    Related Rule:
    
*/


#include "ntddk_slic.h"

state{
   enum {normalTimer, periodicTimer} t = normalTimer;
} 

sdv_NdisSetTimerObject.exit
{
    if ($1 == g_TimerObject)
    {
      if ($3 == 0) {
          t = normalTimer;
      }
      else if ($3 > 0) {
          t = periodicTimer;
      } 
      else
      {
        abort "Invalid parameter passed to $fname";
      }
    }
   
}

sdv_NdisCancelTimerObject.entry
{
    if ($1 == g_TimerObject)
    {
      if ((t == periodicTimer) && (sdv_irql_current != PASSIVE_LEVEL))
      {
          abort "Periodic timer must be cancelled at IRQL = PASSIVE";
      }
      else if ((t == normalTimer) && SDV_IRQL_ELEVATED_LEVEL(sdv_irql_current))
      {
          abort "Non-periodic timer must be cancelled at IRQL <= DISPATCH";
      } 
    }
}
